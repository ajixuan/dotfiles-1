" HACK(strager): Work around LC_ALL=C setting encoding=latin1.
" NOTE(strager): This must come before :scriptencoding. See :help
" scriptencoding.
set encoding=utf-8

scriptencoding utf-8

""" General
set nocompatible

" Work around CVE-2019-12735.
set nomodeline

""" Plugins
call strager#help#register_command({'force': v:false})
call strager#move_file#register_command({'force': v:false})

au BufEnter *.hs compiler ghcx

packloadall

""" Whitespace, indention, etc.
set sw=4 et
set softtabstop=-1
set ts=8
set nosmartindent
set cin noai
set tw=60 cc=60
set nojoinspaces
set formatoptions=cloqr

" g:StragerGetCStyleComments returns a list of 'comments parts. Use this
" function in ftplugin-s.
function g:StragerGetCStyleComments()
  return ['sO:*\ -', 'mO:*\ \ ', 'exO:*/', 's1:/*', 'mb:*', 'ex:*/', '://']
endfunction

" Copyright Saleem Abdulrasool <compnerd@compnerd.org>
command! GNUFormat :setlocal
    \ cinoptions=>2s,n-1s,{s,^-1s,:1s,=1s,g0,h1s,t0,+1s,(0,u0,w1,m1
    \ noexpandtab
    \ shiftwidth=2
    \ softtabstop=2
    \ tabstop=8

""" Text manipulation
set bs=indent,eol,start
set completeopt=menu,preview

" Don't synchronize Vim's unnamed register with system clipboard.
set clipboard=

""" Text alignment
""" http://vim.wikia.com/wiki/Regex-based_text_alignment
command! -nargs=? -range Align <line1>,<line2>call AlignSection('<args>')
function! AlignSection(regex) range
  let extra = 1
  let sep = empty(a:regex) ? '=' : a:regex
  let maxpos = 0
  let section = getline(a:firstline, a:lastline)
  for line in section
    let pos = match(line, ' *'.sep)
    if maxpos < pos
      let maxpos = pos
    endif
  endfor
  call map(section, 'AlignLine(v:val, sep, maxpos, extra)')
  call setline(a:firstline, section)
endfunction

function! AlignLine(line, sep, maxpos, extra)
  let m = matchlist(a:line, '\(.\{-}\) \{-}\('.a:sep.'.*\)')
  if empty(m)
    return a:line
  endif
  let spaces = repeat(' ', a:maxpos - strlen(m[1]) + a:extra)
  return m[1] . spaces . m[2]
endfunction

" Make Y consistent with D (i.e. D : d$ :: Y : y$)
nmap <unique> Y y$

""" Navigation
set nofoldenable
set foldmethod=marker
if v:version >= 703
    set relativenumber
endif
set numberwidth=3
set nostartofline

autocmd CmdwinEnter * setlocal norelativenumber

" Sane searching
set hlsearch
set incsearch
set smartcase ignorecase
nmap <silent> <unique> <Leader>l :nohlsearch<CR>

" Save searches
set viminfo+=/200

" :grep
command -complete=dir -nargs=+
  \ Grep silent grep <args>
  \ | redraw!
  \ | call strager#window#open_quickfix_window()
set grepformat=%f:%l:%c:%m
set grepformat+=%-GNo\ files\ were\ searched\\,\ which\ means\ ripgrep\ probably\ applied\ a\ filter\ you\ didn't\ expect.\ Try\ running\ again\ with\ --debug.
set grepprg=rg
if has('unix')
  set grepprg+=\ \</dev/null
endif
set grepprg+=\ --color\ never\ --column\ --line-number\ --no-heading\ $*
nmap <unique> <Leader>a :Grep<SPACE>

" Use space to find space
" (Note the space after f, F)
" Kinda broken right now...
nmap <unique> <Space> ef<Space>

" Use tab for %
nnoremap <unique> <tab> %
vnoremap <unique> <tab> %

" Navigate jump list with \j, \k
nnoremap <unique> <Leader>j <C-I>
nnoremap <unique> <Leader>k <C-O>

" Go-to-definition
nmap <unique> <C-]> :call strager#tag#go()<CR>

""" Window and tab management
" Windows
nmap <unique> th <C-W>h
nmap <unique> tl <C-W>l
nmap <unique> tj <C-W>j
nmap <unique> tk <C-W>k
nmap <unique> ts :split<SPACE>
nmap <unique> tv :vsplit<SPACE>
nmap <unique> tc <C-W>c
set splitbelow splitright

" Tabs
" Use <Leader>c to pop the "tab stack".
nmap <unique> <Leader>c :tabclose<CR>

let s:i = 1
while s:i <= 10
  execute printf(
    \ 'nnoremap <unique> <Leader>%d :%dtabnext<CR>',
    \ s:i == 10 ? 0 : s:i,
    \ s:i,
  \ )
  let s:i += 1
endwhile

""" File management
set autowrite 

" Dirvish directory browser
command! Explore echoerr 'Stop using :Ex. Use - instead!'
let g:dirvish_relative_paths = 1
let g:dirvish_mode = ':call strager#file_sort#sort_current_buffer()'
let g:loaded_netrwPlugin = 1
autocmd BufWritePost * call strager#directory_browser#refresh_open_browsers()
call strager#directory_browser#register_commands()

" Don't hide any files from completion.
set wildignore=

" Put swap files in ~/.vim/swap/.  Note the double /:
"
" > - For Unix and Win32, if a directory ends in two path separators "//"
" >  or "\\", the swap file name will be built from the complete path to
" >  the file with all path separators substituted to percent '%' signs.
" >  This will ensure file name uniqueness in the preserve directory.
set directory=~/.vim/swap//,.

" fzf
nmap <unique> <Leader>b :call strager#search_buffers#search_using_fzf()<CR>
nmap <unique> <C-P> :call strager#search_files#search_using_fzf()<CR>

""" Display
set nowrap
set ruler
set laststatus=2
set list
set listchars=
set listchars+=precedes:<,extends:>
set listchars+=tab:â‡¥\ 
set sidescroll=5
set scrolloff=5
set shortmess=a     " Abbreviate status line
set shortmess+=tToO " Other crap

let loaded_matchparen = v:true

call strager#color_column#set_up_sync_colorcolumn_with_textwidth()

call strager#cursor_shape#set_cursor_shapes({
  \ 'insert': 'vertical bar',
  \ 'normal': 'block',
  \ 'replace': 'underline',
\ })

" filename [Help][-][RO]
set statusline=\ \ \ %<%f\ %h%m%r
" Line, col
set statusline+=%=%(%l,%c%V%)

" Terminal window title
set title
set titlestring=%t
set titleold=

""" Spelling
set spellcapcheck=

""" Command line
set wildmenu
set wildmode=longest,full

" Save history
set history=10000
set viminfo+=:10000

""" Terminal
" Treat undercurl as underline.
set t_Cs= t_Ce=

" Handle <ESC> immediately in insert mode. Wait indefinitely for incomplete
" mappings.
set esckeys
set notimeout ttimeout ttimeoutlen=0
set showcmd

""" Syntax hilighting
syntax on
filetype on
filetype indent on
filetype plugin on

au BufNewFile *.as set filetype=javascript
au BufRead *.as set filetype=javascript

set background=dark
let g:solarized_termtrans=1
colorscheme strager

" Make the vertical split column and the status line blend
" with the line number column. See also colors/strager.vim.
set fillchars+=vert:\ 
" FIXME(strager): This overrides the previous line...
set fillchars=stl:\ ,stlnc:\ 

let c_space_errors=1
let c_no_comment_fold=1
let c_no_if0_fold=1

set concealcursor=nv

""" Shortcuts
map <unique> <F1> <ESC>:make<CR>
set makeprg=make\ -j4\ -w
nmap <unique> <Leader>s vip:sort u<CR>
vmap <unique> <Leader>s :sort u<CR>
nmap <unique> <Leader>S vip:sort iu<CR>
vmap <unique> <Leader>S :sort iu<CR>
map <unique> <Leader>w :wa<CR>

command -range GistLinesToChat call system('chatgist', getbufline('%', <line1>, <line2>))
vnoremap <unique> <F2> :GistLinesToChat<CR>

" Turn off the K command. I keep accidentally triggering it, and I don't care
" for its behavior right now.
nnoremap <unique> K <Nop>

" Paste toggle
nmap <unique> <Leader>p :set paste!<CR>

""" Linting
let g:ale_echo_cursor = v:false
let g:ale_lint_delay = 0
let g:ale_linters_explicit = v:true
let g:ale_set_signs = v:false

" Must be last.  Forgot why.
set exrc secure
