""" General
set nocompatible

""" Plugins
" Let vimballs extract themselves in bundle/out/
" so we can play with them ourselves.
let g:vimball_home=$HOME."/.vim/bundle/out"

" Log Vim commands
if has('cmdlog')
    set cmdlogdir=~/.vimlogs/
    " set cmdloginsert
end

" Haskell mode
"let g:haddock_browser="nothing"

au BufEnter *.hs compiler ghcx

" Ack.vim
"let g:ackprg="ack-grep -H --nocolor --nogroup --column"
"let g:ackprg="ack -H --nocolor --nogroup --column"
let g:ackprg="ag --nocolor --nogroup --column"
nmap <Leader>a :Ack<SPACE>

" Pathogen, activate!
call pathogen#infect()

""" Whitespace, indention, etc.
set sw=4 sts=4 et
set ts=8
set nosmartindent
set cin noai
set tw=60 cc=60
set nojoinspaces

" Copyright Saleem Abdulrasool <compnerd@compnerd.org>
command! GNUFormat :setlocal
    \ cinoptions=>2s,n-1s,{s,^-1s,:1s,=1s,g0,h1s,t0,+1s,(0,u0,w1,m1
    \ noexpandtab
    \ shiftwidth=2
    \ softtabstop=2
    \ tabstop=8

""" Text manipulation
set bs=indent,eol,start
set completeopt=menu,preview
set noesckeys

" Synchronize default Vim register and system clipboard.
set clipboard=unnamed

""" Text alignment
""" http://vim.wikia.com/wiki/Regex-based_text_alignment
command! -nargs=? -range Align <line1>,<line2>call AlignSection('<args>')
function! AlignSection(regex) range
  let extra = 1
  let sep = empty(a:regex) ? '=' : a:regex
  let maxpos = 0
  let section = getline(a:firstline, a:lastline)
  for line in section
    let pos = match(line, ' *'.sep)
    if maxpos < pos
      let maxpos = pos
    endif
  endfor
  call map(section, 'AlignLine(v:val, sep, maxpos, extra)')
  call setline(a:firstline, section)
endfunction

function! AlignLine(line, sep, maxpos, extra)
  let m = matchlist(a:line, '\(.\{-}\) \{-}\('.a:sep.'.*\)')
  if empty(m)
    return a:line
  endif
  let spaces = repeat(' ', a:maxpos - strlen(m[1]) + a:extra)
  return m[1] . spaces . m[2]
endfunction

" Make Y consistent with D (i.e. D : d$ :: Y : y$)
nmap Y y$

""" Navigation
set foldmethod=marker
if v:version >= 703
    set relativenumber
endif

" Sane searching
set hlsearch
set incsearch
set smartcase ignorecase
nmap <Leader>l :nohlsearch<CR>

" Save searches
set viminfo+=/200

" Alt-j/k to navigate forward/backward in the tags stack
map <M-J> <C-T>
map <M-K> <C-]>

" Use space to find space
" (Note the space after f, F)
" Kinda broken right now...
nmap <Space> ef<Space>

" Use tab for %
nnoremap <tab> %
vnoremap <tab> %

" Navigate jump list with \j, \k
nnoremap <Leader>j <C-I>
nnoremap <Leader>k <C-O>

""" Window and tab management
" Windows
nmap th <C-W>h
nmap tl <C-W>l
nmap tj <C-W>j
nmap tk <C-W>k
nmap ts :split<SPACE>
nmap tv :vsplit<SPACE>
nmap tc <C-W>c
set splitbelow splitright

" Tabs
nmap Th :tabp<CR>
nmap Tl :tabn<CR>
nmap Te :tabe<SPACE>
nmap Tn :tabe .<CR>
nmap Tc :tabc<CR>

nmap TH :tabp<CR>
nmap TL :tabn<CR>
nmap TE :tabe<SPACE>
nmap TN :tabe .<CR>
nmap TC :tabc<CR>

nmap tH :tabp<CR>
nmap tL :tabn<CR>
nmap tE :tabe<SPACE>
nmap tN :tabe .<CR>
nmap tC :tabc<CR>

nmap te :tabe<SPACE>

""" File management
set autowrite 

" wildignore ('wig is cooler to write)
" Version control
set wig+=*/.git/*,*/.svn/*,*/_darcs/*
" Build/package systems
set wig+=*/node_modules/*,*/dist/*,*/build/*,*/.build/*,*/cabal-dev/*,*.pyc
" Vim bullshit
set wig+=*.swp,*.swa

" Put swap files in ~/.vim/swap/.  Note the double /:
"
" > - For Unix and Win32, if a directory ends in two path separators "//"
" >  or "\\", the swap file name will be built from the complete path to
" >  the file with all path separators substituted to percent '%' signs.
" >  This will ensure file name uniqueness in the preserve directory.
set directory=~/.vim/swap//,.

" fzf
" TODO(strager): Move $FZF_DEFAULT_OPTS outside Vim.
let $FZF_DEFAULT_OPTS = '--no-progress --inline-info'
nmap ' :Buffers<CR>
nmap <C-P> :Files<CR>

""" Display
set nowrap
set ruler
set laststatus=2
set listchars+=precedes:<,extends:>
set sidescroll=5
set scrolloff=5
set shortmess=a     " Abbreviate status line
set shortmess+=tToO " Other crap

" filename [Help][-][RO]
set statusline=%<%f\ %h%m%r
" Line, col
set statusline+=%=%(%l,%c%V%)

""" Command line
set wildmenu
set wildmode=longest,full

set tags+=/usr/local/share/ctags/qt4

" Save history
set history=200
set viminfo+=:200

""" Syntax hilighting
syntax on
filetype on
filetype indent on
filetype plugin on

au BufNewFile *.as set filetype=javascript
au BufRead *.as set filetype=javascript

" Prefer 8-color over 256-color. The colour scheme is tuned
" for 8-color with iTerm.
if &term =~ "xterm-256color"
  set term=xterm
endif

set background=dark
let g:solarized_termtrans=1
colorscheme solarized

highlight StatusLine ctermfg=82
highlight StatusLineNC ctermfg=81
highlight VertSplit ctermfg=16

let c_space_errors=1
let c_no_comment_fold=1
let c_no_if0_fold=1

set concealcursor=nv

""" Shortcuts
map <F1> <ESC>:make<CR>
set makeprg=make\ -j4\ -w
map <F9> :!ctags -R --c++-kinds=+p --fields=+iaS --extra=+q .<CR>
nmap <Leader>s vip:sort u<CR>
vmap <Leader>s :sort u<CR>
nmap <Leader>S vip:sort iu<CR>
vmap <Leader>S :sort iu<CR>
map <Leader>w :wa<CR>

" Git
nmap gs :!git status -s -b .<CR>
nmap gS :!git status -s -b<CR>
nmap gc :!git commit -v<CR>
nmap g. :!git add -p .<CR>
nmap g; :!git add -i .<CR>
nmap g? :!git diff .<CR>
nmap g/ :!git diff --cached .<CR>
nmap gv :!git pull --ff --commit<CR>
nmap g^ :!git push<CR>
nmap gP :!git checkout -p .<CR>

set shell=/bin/bash

" Paste toggle
nmap <Leader>p :set paste!<CR>

function! s:escape_sub(s)
  " See |sub-replace-special| and 'magic.
  return escape(a:s, '&~\\')
endfunction

function! s:get_make_command(makeprg, make_args)
  " See the replace_makeprg C function in vim/src/ex_docmd.c for undocumented
  " behaviour.
  " See 'grepprg/'makeprg for '$*'.
  " See |string-match| for '\$\*'.
  let expanded=substitute(a:makeprg, '\$\*', s:escape_sub(a:make_args), 'g')
  if expanded ==# a:makeprg
    return a:makeprg.' '.a:make_args
  else
    return expanded
  endif
endfunction

function! s:get_make_redirect(shellpipe, shellquote, error_file_path)
  " See the replace_makeprg C function in vim/src/ex_docmd.c for undocumented
  " behaviour.
  " See the append_redir C function in vim/src/ex_cmds.c for undocumented
  " behaviour.
  " TODO(strager): Support empty 'shellpipe.
  " TODO(strager): Support %% in 'shellpipe.
  " TODO(strager): Maybe crash if 'shellpipe is '%s%s'.
  let expanded_shellpipe=substitute(a:shellpipe, '%s', s:escape_sub(a:error_file_path), '')
  if expanded_shellpipe ==# a:shellpipe
    return a:shellpipe.' '.a:error_file_path
  else
    return ' '.expanded_shellpipe
  endif
endfunction

function s:StragerMakeCallback(channel, message)
endfunction

function s:StragerMakeCloseCallback(channel)
endfunction

function s:StragerMakeExitCallback(make_invocation, job, exit_status)
  " TODO(strager): Ensure encoding is correct.

  " TODO(strager): Update 'errorformat.
  " TODO(strager): Update the correct quickfix list.
  " FIXME(strager): This changes 'errorfile.
  " FIXME(strager): This triggers QuickFixCmdPre and QuickFixCmdPost.
  " TODO(strager): Escape. The rules are giving me a headache right now.
  execute 'caddfile '.a:make_invocation['error_file_path']

  " TODO(strager): Trigger QuickFixCmdPost.

  " Automatically jump to the first error if desired.
  " TODO(strager): Do this only once.
  if a:make_invocation['jump_to_first_error']
    " See the ex_make C function in vim/src/quickfix.c.
    let cur_quickfix_list_id=getqflist({'id': 0})['id']
    if cur_quickfix_list_id == a:make_invocation['quickfix_list_id']
      " FIXME(strager): I think this is wrong.
      let quickfix_list_entry_count=getqflist({'size': 0})['size']
      if quickfix_list_entry_count > 0
        cc
      endif
    endif
  endif

  " See the ex_make C function in vim/src/quickfix.c.
  call delete(a:make_invocation['error_file_path'])
endfunction

function s:StragerMake(bang, make_args)
  " TODO(strager): Support 'makeef.
  let error_file_path=tempname()
  " NOTE(strager): Despite what the documentation for :make says, this deletion
  " is unconditional.
  call delete(error_file_path)

  " TODO(strager): Support :grep, etc.
  let command=s:get_make_command(get(b:, 'makeprg', &makeprg), a:make_args)
  let redirect=s:get_make_redirect(&shellpipe, &shellquote, error_file_path)
  " TODO(strager): Support 'shellquote.

  " TODO(strager): Trigger QuickFixCmdPre.
  " TODO(strager): Ensure encoding is correct.
  " TODO(strager): Support 'autowrite and 'autowriteall.

  let make_invocation={
    \ 'error_file_path': error_file_path,
    \ 'jump_to_first_error': !a:bang,
  \ }

  " TODO(strager): Support Windows.
  " FIXME(strager): I think this is wrong. Vim is confusing.
  let exec_command=[&shell,&shellcmdflag,command.redirect]
  let terminal_buffer_number=term_start(exec_command, {
    \ 'callback': function('s:StragerMakeCallback'),
    \ 'close_cb': function('s:StragerMakeCloseCallback'),
    \ 'err_msg': 0,
    \ 'exit_cb': function('s:StragerMakeExitCallback', [make_invocation]),
    \ 'hidden': 1,
    \ 'out_msg': 0,
    \ 'stoponexit': 'term',
    \ 'term_name': command,
  \ })
  if terminal_buffer_number == 0
    " TODO(strager): Handle errors.
    echomsg 'Error in term_start()'
  endif
  let make_invocation['terminal_buffer_number']=terminal_buffer_number

  " TODO(strager): Trigger ShellCmdPost.

  let error_format=get(b:, 'errorformat', &errorformat)
  let make_invocation['error_format']=error_format

  " See the ex_make C function in vim/src/quickfix.c for ':'.
  let quickfix_title=':'.command

  " TODO(strager): Support :grepadd, etc.
  " TODO(strager): Support :lmake, etc.
  " See the ex_make C function in vim/src/quickfix.c for ' ' and 'nr': 0.
  let error=setqflist([], ' ', {
    \ 'efm': error_format,
    \ 'nr': 0,
    \ 'title': quickfix_title,
  \ })
  " TODO(strager): Check for errors.
  let quickfix_list_info=getqflist({'id': 0, 'nr': 0})
  let make_invocation['quickfix_list_id']=quickfix_list_info['id']
endfunction

" BANG = -bang
" EXTRA = ???
" NOTRLCOM = (default; -bar overrides)
" TRLBAR = (default; -bar overrides)
" XFILE = ???
" ADDR_LINES = (unused, I think)
command -bang -complete=file -nargs=* StragerMake call s:StragerMake(<bang>0, <q-args>)

" Must be last.  Forgot why.
set exrc secure
